plugins {
    id 'java-library'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
}
group = 'org.vkkotha'
version = scmVersion.version
archivesBaseName = 'config-utils'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/vkkotha/ConfigUtils"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

task allJar(type: Jar) {
    archiveClassifier = 'all'
    dependsOn configurations.runtimeClasspath
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'org.configutils.PropertiesDiffCli'
    }

    from {
        [
            sourceSets.main.output,
            configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
        ]
    }
}

jar {
    it.dependsOn allJar
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.google.code.gson:gson:2.8.8'
    testImplementation('pl.pragmatists:JUnitParams:1.1.1') {
        exclude group: 'junit'
    }
    testImplementation('junit:junit:4.13.2') {
        exclude group: 'hamcrest'
    }
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.mockito:mockito-core:2.0.2-beta'
}
